# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
pkgname=pkgtools-git
pkgver=20110902
pkgrel=1
pkgdesc="A collection of scripts for Arch Linux packages"
arch=('any')
url="https://github.com/Daenyth/pkgtools"
license=('GPL')
depends=('bash>=4' 'libarchive' 'pcre' 'python')
makedepends=('git')
optdepends=('abs: Provides proto packaging files for newpkg')
provides=('pkgtools')
conflicts=('pkgtools')
backup=('etc/pkgtools/newpkg.conf' 'etc/pkgtools/pkgfile.conf' 'etc/pkgtools/spec2arch.conf')
options=('!strip')
install=pkgtools.install
source=()

_gitroot="git://github.com/Daenyth/pkgtools.git"
_gitname="pkgtools"

build() {
	msg "Connecting to GIT server...."

	if [ -d ${srcdir}/$_gitname ] ; then
		cd $srcdir/$_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	[[ -d $srcdir/$_gitname-build ]] && rm -r ${srcdir}/$_gitname-build
	cp -r ${srcdir}/$_gitname ${srcdir}/$_gitname-build
}

package() {
	cd "$_gitname-build"

	make DESTDIR=${pkgdir} install
}
